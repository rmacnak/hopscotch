class HopscotchTestRunner packageUsing: manifest = (|
Minitest = manifest Minitest.
hopscotchRuntime = manifest HopscotchForHTML5Runtime packageUsing: manifest.
testConfigs = {
	manifest CombinatorialParsingTestingConfiguration packageTestsUsing: manifest.
	manifest NewspeakColorizationTestingConfiguration packageTestsUsing: manifest.
}.
Promise
|) (
private exit: code = (
	(* :pragma: primitive: 189 *)
	panic.
)
public main: platform args: args = (
	| hopscotchPlatform keepAlive stopwatch minitest testModules tester |
	hopscotchPlatform:: hopscotchRuntime using: platform.
	keepAlive:: platform actors Port new.
	Promise:: platform actors Promise.
	stopwatch:: platform time Stopwatch new start.
	minitest:: Minitest usingPlatform: platform.
	testModules:: platform collections List new.
	testConfigs do:
		[:testConfig |
		 testModules addAll:
			(testConfig testModulesUsingPlatform: hopscotchPlatform minitest: minitest)].
	tester:: minitest Tester testModules: testModules.
	tester prepare.

	Promise when: (runTests: tester) fulfilled:
		[(tester successes size printString, ' successes, ',
		 tester failures size printString, ' failures, ',
		 tester errors size printString, ' errors, ',
		 stopwatch elapsedMilliseconds asString, ' ms') out.

		 exit: (tester haveAllTestsSucceeded ifTrue: [0] ifFalse: [1])].
)
runTests: tester = (
	tester atEnd ifTrue: [^self].
	tester peekSelector out.
	^Promise when: tester stepAsync fulfilled:
		[:result |
		 result isError ifTrue:
			[('error: ', result testCase selector) out.
			 result exception out].
		 result isFailure ifTrue:
			[('failure: ', result testCase selector) out.
			 result description out].
		 runTests: tester]
)
) : (
)
